#include "../ShaderCommon.hsf"
#include "../BSDFCommon.hsf"
#include "HybridRenderPipelineCommon.hsf"

#define LIGHTING_SHADER_PER_FRAME_DATA_SLOT                              0
#define LIGHTING_SHADER_GBUFFER0_SRV_SLOT                                1
#define LIGHTING_SHADER_GBUFFER1_SRV_SLOT                                2
#define LIGHTING_SHADER_GBUFFER2_SRV_SLOT                                3
#define LIGHTING_SHADER_GBUFFER3_SRV_SLOT                                4
#define LIGHTING_SHADER_AMBIENT_OCCLUSION_TEXTURE_SRV_SLOT               5
//#define LIGHTING_SHADER_SHADOW_BUFFFER_SRV_SLOT                          5
#define LIGHTING_SHADER_SCENE_COLOR_UAV_SLOT                             6
#define LIGHTING_SHADER_BRDF_LUT_SRV_SLOT                                7
#define LIGHTING_SHADER_SKY_LIGHT_IRRADIANCE_ENVIRONMENT_MAP_SRV_SLOT    8
#define LIGHTING_SHADER_SKY_LIGHT_FILTERED_ENVIRONMENT_MAP_SRV_SLOT      9

PerFrameData GetPerFrameData()
{
    uint bufferIndex = SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_PER_FRAME_DATA_SLOT);
    return BindlessBuffers[(bufferIndex >> 16) & 0xffff].Load<PerFrameData>(bufferIndex & 0xffff);
}

Texture2D GetGBuffer0SRV()
{
    return BindlessTexture2Ds[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_GBUFFER0_SRV_SLOT)];
}

Texture2D GetGBuffer1SRV()
{
    return BindlessTexture2Ds[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_GBUFFER1_SRV_SLOT)];
}

Texture2D GetGBuffer2SRV()
{
    return BindlessTexture2Ds[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_GBUFFER2_SRV_SLOT)];
}

Texture2D GetGBuffer3SRV()
{
    return BindlessTexture2Ds[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_GBUFFER3_SRV_SLOT)];
}

Texture2D GetAmbientOcclusionTextureSRV()
{
    return BindlessTexture2Ds[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_AMBIENT_OCCLUSION_TEXTURE_SRV_SLOT)];
}

RWTexture2D<float4> GetSceneColorUAV()
{
    return BindlessRWTexture2Ds[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_SCENE_COLOR_UAV_SLOT)];
}

Texture2D GetBRDFLutSRV()
{
    return BindlessTexture2Ds[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_BRDF_LUT_SRV_SLOT)];
}

TextureCube GetSkyLightIrradianceEnvironmentMapSRV()
{
    return BindlessTextureCubes[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_SKY_LIGHT_IRRADIANCE_ENVIRONMENT_MAP_SRV_SLOT)];
}

TextureCube GetSkyLightFilteredEnvironmentMapSRV()
{
    return BindlessTextureCubes[SHADER_ARGUMENTS_INDEX(LIGHTING_SHADER_SKY_LIGHT_FILTERED_ENVIRONMENT_MAP_SRV_SLOT)];
}

[numthreads(8, 8, 1)]
void LightingCS(uint3 GlobalInvocationID : SV_DispatchThreadID)
{
	PerFrameData perFrameData = GetPerFrameData();
    uint width = perFrameData.renderResolutionWidth;
    uint height = perFrameData.renderResolutionHeight;
	if (GlobalInvocationID.x >= width || GlobalInvocationID.y >= height) 
	{
		return;
	}
    
    uint2 coord = GlobalInvocationID.xy;

	Texture2D gbuffer0 = GetGBuffer0SRV();
	Texture2D gbuffer1 = GetGBuffer1SRV();
	Texture2D gbuffer2 = GetGBuffer2SRV();
	Texture2D gbuffer3 = GetGBuffer3SRV();

	//Texture2D shadowBuffer = GetShadowBufferSRV();

	float4 gbuffer0Data = gbuffer0[coord];
	float4 gbuffer1Data = gbuffer1[coord];
	float4 gbuffer2Data = gbuffer2[coord];
	float4 gbuffer3Data = gbuffer3[coord];

	float3 position = gbuffer0Data.xyz;
	float3 normal = gbuffer1Data.xyz;
	float4 baseColor = gbuffer2Data;
	float metallic = gbuffer0Data.w;
	float roughness = gbuffer1Data.w;
	float3 emission = gbuffer3Data.rgb;

	float3 F0 = lerp(Fdielectric, baseColor.rgb, metallic);
	float3 finalColor = emission;
	
	//float visibility = shadowBuffer[coord].r;
	float visibility = 1.0;
	float lightIntensity = 1.0;
	float3 N = normal;
	float3 V = normalize(perFrameData.cameraPosition - position);
	float3 R = reflect(-V, N);
	float NdotV = clamp(dot(N, V), 0.0, 1.0);
	float3 irradiance = perFrameData.solarIrradiance;
	float3 Li = -perFrameData.sunDirection;
	float cosLi = clamp(dot(N, Li), 0.0, 1.0);

	{
		// // if(cosLi > 0.0)
		// {
		// 	float3 Lh = normalize(Li + V);
		// 	float cosLh = clamp(dot(N, Lh), 0.0, 1.0);

		// 	float3 F = FresnelSchlick(F0, NdotV);
		// 	float D = D_GGX(cosLh, roughness);
		// 	float G = SchlickGGX(cosLi, NdotV, roughness);
		// 	float3 Kd = (1.0 - F) * (1.0 - metallic);

		// 	float3 diffuse = Kd * baseColor.rgb / PI;
		// 	float3 specular = (F * D * G) / max(0.001, 4.0 * cosLi * NdotV);
		// 	float3 radiance = irradiance * lightIntensity;

		// 	finalColor += (diffuse + specular) * radiance * cosLi * visibility;
		// }
	}

	Texture2D ambientOcclusionTexture = GetAmbientOcclusionTextureSRV();
	float ambientOcclusion = ambientOcclusionTexture[coord].r;
	// IBL
	{
		Texture2D brdfLut = GetBRDFLutSRV();
		TextureCube irradianceEnvironmentMap = GetSkyLightIrradianceEnvironmentMapSRV();
		TextureCube filteredEnvironmentMap = GetSkyLightFilteredEnvironmentMapSRV();

		float3 F = FresnelSchlickRoughness(F0, NdotV, roughness);
		float3 Kd = (1.0 - F) * (1.0 - metallic);

		float3 irradiance = irradianceEnvironmentMap.SampleLevel(SAMPLER_LINEAR_WARP, N, 0).rgb;
		// Diffuse based on irradiance
		float3 diffuse = Kd * baseColor.rgb * irradiance;
		
		uint w; uint h; uint numMipLevels;
		filteredEnvironmentMap.GetDimensions(0, w, h, numMipLevels);
		float lod = roughness * (numMipLevels - 1);
		float3 prefilteredColor = filteredEnvironmentMap.SampleLevel(SAMPLER_LINEAR_WARP, R, lod).rgb;

		// @note When loading the texture, use CLAMP_TO_EDGE to ensure that values are read correctly.
		float2 brdf = brdfLut.SampleLevel(SAMPLER_LINEAR_CLAMP, float2(NdotV, roughness), 0).rg;
		// Specular reflection
		float3 specular = (F0 * brdf.x + brdf.y) * prefilteredColor;
		
		finalColor += (diffuse + specular);
	}

	RWTexture2D<float4> sceneColor = GetSceneColorUAV();
	sceneColor[coord] = float4(finalColor, 1.0);
}
